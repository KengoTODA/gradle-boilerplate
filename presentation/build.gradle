apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.siouan.frontend-jdk11'

apply from: "$rootDir/gradle/publish.gradle"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation project(':application')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation project(':infrastructure')
}

def copyFrontendResources = tasks.register('copyFrontendResources', Copy)

copyFrontendResources.configure {
    from tasks.named('assembleFrontend')
    into "$buildDir/resources/main/public"
}

tasks.named('processResources').configure {
    dependsOn copyFrontendResources
}

frontend {
    // use the node installed by nvm
    nodeDistributionProvided = true

    assembleScript = 'run assemble'
    checkScript = 'run check'
    cleanScript = 'run clean'
}

tasks.named('assembleFrontend').configure {
    inputs.dir 'src'
    inputs.files '*.json'
    outputs.dir 'dist'
}

tasks.named('checkFrontend').configure {
    inputs.dir 'src'
    inputs.dir 'test'
    inputs.files '*.json'
    inputs.file 'jest.config.js'
}
