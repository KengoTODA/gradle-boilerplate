apply plugin: 'maven-publish'

def getBranch() {
    def process = 'git branch --show-current'.execute()
    process.waitFor()
    return process.text.trim()
}

def getHash() {
    def process = 'git rev-parse HEAD'.execute()
    process.waitFor()
    return process.text.trim()
}

bootJar {
    manifest {
        attributes (
                'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Git-Branch': getBranch(),
                'Git-Hash': getHash()
                )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'jp.skypencil'
            from components.java
        }
    }
}

// TODO: sign for stable release
// apply plugin: 'signing'
// signing { sign publishing.publications }
