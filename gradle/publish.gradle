apply plugin: 'maven-publish'
apply plugin: 'com.google.cloud.tools.jib'

def getBranch() {
    def process = 'git branch --show-current'.execute()
    process.waitFor()
    return process.text.trim()
}

def getHash() {
    def process = 'git rev-parse HEAD'.execute()
    process.waitFor()
    return process.text.trim()
}

bootJar {
    manifest {
        attributes (
                'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Git-Branch': getBranch(),
                'Git-Hash': getHash()
                )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'jp.skypencil'
            from components.java
        }
    }
}

jib {
    from {
        image ='adoptopenjdk:16-jre'
        auth {
            username = providers.environmentVariable('DOCKERHUB_USER')
            password = providers.environmentVariable('DOCKERHUB_PASSWORD')
        }
    }
    to {
        image = 'docker.pkg.github.com/kengotoda/gradle-boilerplate/gradle-boilerplate'
        auth {
            username = providers.environmentVariable('GITHUB_ACTOR')
            password = providers.environmentVariable('GITHUB_TOKEN')
        }
    }
}

// TODO: sign for stable release
// apply plugin: 'signing'
// signing { sign publishing.publications }
