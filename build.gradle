plugins {
    id 'java'
    id 'com.diffplug.spotless' version '5.9.0' apply false
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'net.ltgt.errorprone' version '1.3.0' apply false
}

ext {
    junitVersion = '5.7.0'
    guavaVersion = '29.0-jre'
    immutablesVersion = '2.8.9-ea-1'
    errorproneVersion = '2.5.1'
}

apply plugin: 'java-library'
apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/publish.gradle"

allprojects {
    repositories { mavenCentral() }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation project(':domain')
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly "org.immutables:value:${immutablesVersion}:annotations"
    annotationProcessor "org.immutables:value:${immutablesVersion}"
    testImplementation "com.google.guava:guava:${guavaVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

subprojects {
    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/test.gradle"
    apply from: "$rootDir/gradle/spotless.gradle"

    ext {
        mavenBom = org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

task jacocoRootReport(type: JacocoReport) {
    description = 'Merge all coverage reports before submit to SonarQube'
    def reportTasks = project.getTasksByName("jacocoTestReport", true)
    dependsOn reportTasks

    executionData.setFrom reportTasks.executionData
    sourceDirectories.setFrom reportTasks.sourceDirectories

    reports {
        html.enabled = true
    }
}

defaultTasks 'spotlessApply', 'build'
